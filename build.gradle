plugins {
    id 'org.springframework.boot' version '2.2.6.RELEASE'
    id 'io.spring.dependency-management' version '1.0.9.RELEASE'
    id 'org.asciidoctor.convert' version '1.5.8'
    id 'org.flywaydb.flyway' version '6.3.3'
    id 'java'
}

version = '0.0.1'
group = 'com.analuciabolico'
compileJava.options.encoding = 'UTF-8'
sourceCompatibility = JavaVersion.VERSION_14
targetCompatibility = JavaVersion.VERSION_14


repositories {
    mavenCentral()
}

ext {
    set('flywayVersion', '6.3.3')
    set('swaggerVersion', '2.9.2')
    set('springCloudVersion', "Hoxton.SR3")
    set('springBootVersion', '2.2.6.RELEASE')
    set('snippetsDir', file("build/generated-snippets"))
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-cache'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.cloud:spring-cloud-starter-config'
    implementation 'org.springframework.cloud:spring-cloud-starter-openfeign'
    implementation 'org.springframework.boot:spring-boot-starter-activemq'
    implementation 'org.flywaydb:flyway-core:' + flywayVersion
    implementation 'io.springfox:springfox-swagger-ui:' + swaggerVersion
    implementation 'io.springfox:springfox-swagger2:' + swaggerVersion
    compileOnly 'org.projectlombok:lombok'
    runtimeOnly 'com.h2database:h2'
    runtimeOnly 'org.postgresql:postgresql'
    annotationProcessor 'org.projectlombok:lombok'
    testImplementation 'com.h2database:h2'
    testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'
    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}

configurations {
    developmentOnly
    runtimeClasspath {
        extendsFrom developmentOnly
    }
    compileOnly {
        extendsFrom annotationProcessor
    }
}

test {
    outputs.dir snippetsDir
    useJUnitPlatform()
}

asciidoctor {
    inputs.dir snippetsDir
    dependsOn test
}

springBoot {
    buildInfo {
        properties {
            artifact = 'assembly'
            version = '0.0.1'
            group = 'com.analuciabolico'
            name = 'Application Spring Boot'
        }
    }
}